// SPDX-FileCopyrightText: 2026 Leitwolf <xs-lang.chess031@slmails.com>
// SPDX-License-Identifier: Apache-2.0

// Complete-language example program:
// Scans a content directory and emits a small static-site health report.



import collections, fs, optional, stdio, process;

data PageInfo {
    path: Str;
    title: Str;
    word_count: Int;
}

class Markdown {
    static fn title(path: Str, text: Str) -> Result<Str, Error> {
        for (line: Str in text.lines()) {
            if (line.starts_with("# ")) {
                return Ok(line.trim_start("# ").trim());
            }
        }

        return Error(new Error("missing page title"));
    }

    static fn count_words(text: Str) -> Int {
        return text.split_whitespace().length();
    }
}

class SiteReport {
    pages: std::collections::Vector<PageInfo>;

    SiteReport() {
        self.pages = std::collections::Vector<PageInfo>::new();
    }

    fn add_markdown(path: Str) -> Result<()> {
        text: Str = std::fs::read_to_str(path);
        self.pages.push(PageInfo {
            path: path,
            title: Markdown::title(path, text)@,
            word_count: Markdown::count_words(text),
        });
        return Ok();
    }

    fn print() -> Result<()> {
        println!("pages: {}", self.pages.length());

        for (page: PageInfo in self.pages) {
            println!("{:<32} {:>6} {}", page.title, page.word_count, page.path);
        }
        return Ok();
    }
}

fn main(args: std::process::Args) -> Result<Int, Error> {
    root: Str = if (args.length() == 2) {
        args[1]
    }
    else {
        "."
    };

    report: SiteReport = new SiteReport();
    for (path: Str in std::fs::walk(root)) {
        if (path.ends_with(".md")) {
            report.add_markdown(path)@;
        }
    }

    report.print()@;
    return Ok(0);
}
